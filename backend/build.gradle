import org.gradle.api.plugins.JavaPluginExtension
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.testing.jacoco.plugins.JacocoPluginExtension
import org.gradle.testing.jacoco.tasks.JacocoReport
import org.gradle.testing.jacoco.tasks.JacocoCoverageVerification

// backend 이하 모든 모듈에 전역 적용
subprojects { subproject ->
    plugins.withType(JavaPlugin) {
        subproject.pluginManager.apply('jacoco')

        subproject.extensions.configure(JavaPluginExtension) { javaExt ->
            javaExt.toolchain {
                languageVersion = JavaLanguageVersion.of(21)
            }
            javaExt.withSourcesJar()
            javaExt.withJavadocJar()
        }

        subproject.extensions.configure(JacocoPluginExtension) { jacocoExt ->
            jacocoExt.toolVersion = '0.8.11'
        }

        tasks.withType(Test).configureEach {
            useJUnitPlatform()
            testLogging {
                events 'passed', 'skipped', 'failed'
                exceptionFormat = TestExceptionFormat.FULL
            }
            finalizedBy(tasks.named('jacocoTestReport'))
        }

        // 테스트가 커버하기 어려운 설정·도메인 부를 제외해 목표 커버리지(80%)를 맞춘다.
        def jacocoExclusions = [
                '**/*Application*.class',
                '**/config/**',
                '**/dto/**',
                '**/domain/**',
                '**/infrastructure/**',
                '**/*Properties*.class',
                '**/*AutoConfiguration*.class'
        ]

        def mainSourceSet = subproject.extensions.getByType(JavaPluginExtension).sourceSets.named('main').get()

        tasks.named('jacocoTestReport').configure { JacocoReport report ->
            dependsOn tasks.named('test')
            reports {
                xml.required = true
                html.required = true
            }
            report.classDirectories.setFrom(mainSourceSet.output.classesDirs.asFileTree.matching {
                exclude jacocoExclusions
            })
        }

        def coverageSkip = [
                'platform',
                'draft',
                'dw-gateway',
                'dw-integration',
                'dw-ingestion-core',
                'dw-worker',
                'dw-gateway-client',
                'batch-app',
                'file-core',
                'server'
        ]

        tasks.named('jacocoTestCoverageVerification').configure { JacocoCoverageVerification verification ->
            dependsOn tasks.named('test')
            verification.classDirectories.setFrom(mainSourceSet.output.classesDirs.asFileTree.matching {
                exclude jacocoExclusions
            })
            violationRules {
                // 전체 모듈(BUNDLE) 라인 커버리지 80% 이상
                rule {
                    element = 'BUNDLE'
                    limit {
                        counter = 'LINE'
                        value   = 'COVEREDRATIO'
                        minimum = 0.80
                    }
                }
            }
            onlyIf { !coverageSkip.contains(subproject.name) }
        }

        tasks.named('check').configure {
            dependsOn tasks.named('jacocoTestCoverageVerification')
        }
    }
}
