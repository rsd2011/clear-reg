plugins {
    id 'java-library'
}

dependencies {
    implementation platform(libs.spring.boot.bom)
    testImplementation platform(libs.spring.boot.bom)

    implementation project(':backend:platform')
    implementation project(':backend:audit')
    implementation libs.spring.boot.starter
    implementation libs.spring.boot.starter.data.jpa
    implementation libs.spring.boot.starter.validation
    implementation libs.jackson.dataformat.yaml
    implementation libs.jackson.datatype.jsr310
    implementation libs.poi.ooxml
    implementation libs.pdfbox

    compileOnly libs.lombok
    annotationProcessor libs.lombok
    testCompileOnly libs.lombok
    testAnnotationProcessor libs.lombok

    testImplementation libs.bundles.spring.test
    testImplementation libs.junit.platform.launcher
    testImplementation testFixtures(project(':backend:platform'))
    testRuntimeOnly libs.h2.database
}

// 커버리지 목표는 export 경로 검증(P2 마스킹 커버리지 보강)에 집중한다.
// 나머지 DW 적재/리드모델/도메인 영역은 별도 통합테스트 범위로 남겨 두기 위해 제외한다.
def dwJacocoExcludes = [
        'com/example/dw/**',
]
def dwJacocoIncludes = [
        'com/example/dw/application/export/**'
]

tasks.named('jacocoTestReport').configure {
    classDirectories.setFrom(classDirectories.files.collect { dir ->
        fileTree(dir: dir, include: dwJacocoIncludes, exclude: dwJacocoExcludes)
    })
}

tasks.named('jacocoTestCoverageVerification').configure {
    // TODO: 커버리지 보강 후 90/80으로 상향 예정
    enabled = true
    classDirectories.setFrom(classDirectories.files.collect { dir ->
        fileTree(dir: dir, include: dwJacocoIncludes, exclude: dwJacocoExcludes)
    })
    violationRules {
        rules.each { it.enabled = false } // 글로벌 기본 룰 비활성화 후 재정의
        rule {
            element = 'BUNDLE'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.55
            }
        }
    }
}
