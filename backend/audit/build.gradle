plugins {
    id 'java-library'
}

dependencies {
    implementation platform(libs.spring.boot.bom)
    testImplementation platform(libs.spring.boot.bom)

    implementation project(':backend:platform')

    // 부트 스타터 제거: 코어 + 오토컨피규어
    implementation 'org.springframework.boot:spring-boot'
    implementation 'org.springframework.boot:spring-boot-autoconfigure'

    // 스프링 코어/데이터/웹/AOP
    implementation libs.spring.context
    implementation libs.spring.data.commons
    implementation 'org.springframework.data:spring-data-jpa'
    implementation 'org.springframework:spring-orm'
    implementation 'org.springframework:spring-tx'
    implementation libs.jakarta.persistence
    implementation 'org.springframework:spring-webmvc'
    implementation 'org.springframework:spring-aop'
    implementation 'org.aspectj:aspectjweaver'

    // 검증
    implementation 'org.hibernate.validator:hibernate-validator'
    runtimeOnly 'org.glassfish:jakarta.el:4.0.2'

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    implementation libs.spring.kafka
    implementation libs.caffeine
    implementation libs.jackson.databind
    implementation libs.jackson.datatype.jsr310

    // SpotBugs @SuppressFBWarnings
    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.8.6'

    compileOnly libs.lombok
    annotationProcessor libs.lombok
    testCompileOnly libs.lombok
    testAnnotationProcessor libs.lombok

    testImplementation libs.bundles.spring.test
    testImplementation libs.junit.platform.launcher
    testImplementation libs.spring.kafka.test
    testImplementation 'com.h2database:h2'
}


// 테스트 유틸(SelfSignedCertGenerator)이 JDK 내부 sun.security.* API를 사용하므로
// JDK 21 모듈 엑스포트 플래그를 테스트 컴파일/실행에만 한정해 추가한다.
tasks.withType(JavaCompile).matching { it.name.toLowerCase().contains('test') }.configureEach {
    options.compilerArgs += ['--add-exports=java.base/sun.security.x509=ALL-UNNAMED',
                             '--add-exports=java.base/sun.security.util=ALL-UNNAMED']
}
tasks.withType(Test).configureEach {
    jvmArgs += ['--add-exports=java.base/sun.security.x509=ALL-UNNAMED',
                '--add-exports=java.base/sun.security.util=ALL-UNNAMED']
}
