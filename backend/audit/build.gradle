plugins {
    id 'java-library'
}

dependencies {
    implementation platform(libs.spring.boot.bom)
    testImplementation platform(libs.spring.boot.bom)

    implementation project(':backend:platform')
    implementation libs.spring.boot.starter
    implementation libs.spring.boot.starter.validation
    implementation libs.spring.boot.starter.data.jpa
    implementation libs.spring.kafka
    implementation libs.caffeine
    implementation libs.jackson.databind
    implementation libs.jackson.datatype.jsr310
    implementation libs.spring.boot.starter.web

    compileOnly libs.lombok
    annotationProcessor libs.lombok
    testCompileOnly libs.lombok
    testAnnotationProcessor libs.lombok

    testImplementation libs.bundles.spring.test
testImplementation libs.junit.platform.launcher
testImplementation libs.spring.kafka.test
testImplementation 'com.h2database:h2'
}

def auditJacocoExcludes = [
        '**/infra/kafka/AuditKafkaProperties.*',
        '**/infra/siem/**'
]

tasks.named('jacocoTestReport').configure {
    classDirectories.setFrom(classDirectories.files.collect { dir ->
        fileTree(dir: dir, exclude: auditJacocoExcludes)
    })
}

tasks.named('jacocoTestCoverageVerification').configure {
    classDirectories.setFrom(classDirectories.files.collect { dir ->
        fileTree(dir: dir, exclude: auditJacocoExcludes)
    })
    violationRules {
        rules.each { it.enabled = false }
        rule {
            element = 'BUNDLE'
            includes = ['com.example.audit*', 'com.example.audit.infra*', 'com.example.audit.drm*']
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.90
            }
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.75
            }
        }
    }
}
