plugins {
    id 'java-library'
}

dependencies {
    implementation platform(libs.spring.boot.bom)
    testImplementation platform(libs.spring.boot.bom)

    implementation project(':backend:platform')

    // 부트 스타터 제거: 코어 + 오토컨피규어
    implementation libs.spring.boot.lib
    implementation libs.spring.boot.autoconfigure

    // 스프링 코어/데이터/웹/AOP
    implementation libs.spring.context
    implementation libs.spring.data.commons
    implementation libs.spring.data.jpa
    implementation libs.spring.orm
    implementation libs.spring.tx
    implementation libs.jakarta.persistence
    implementation libs.spring.webmvc
    implementation libs.spring.aop
    implementation libs.aspectjweaver

    // 검증
    implementation libs.hibernate.validator
    runtimeOnly libs.jakarta.el

    compileOnly libs.findbugs.jsr305

    implementation libs.spring.kafka
    implementation libs.caffeine
    implementation libs.jackson.databind
    implementation libs.jackson.datatype.jsr310

    // SpotBugs @SuppressFBWarnings
    compileOnly libs.spotbugs.annotations

    compileOnly libs.lombok
    annotationProcessor libs.lombok
    testCompileOnly libs.lombok
    testAnnotationProcessor libs.lombok

    testImplementation libs.bundles.spring.test
    testImplementation libs.junit.platform.launcher
    testImplementation libs.spring.kafka.test
    testImplementation libs.h2.database
}


// 테스트 유틸(SelfSignedCertGenerator)이 JDK 내부 sun.security.* API를 사용하므로
// JDK 21 모듈 엑스포트 플래그를 테스트 컴파일/실행에만 한정해 추가한다.
tasks.withType(JavaCompile).matching { it.name.toLowerCase().contains('test') }.configureEach {
    options.compilerArgs += ['--add-exports=java.base/sun.security.x509=ALL-UNNAMED',
                             '--add-exports=java.base/sun.security.util=ALL-UNNAMED']
}
tasks.withType(Test).configureEach {
    jvmArgs += ['--add-exports=java.base/sun.security.x509=ALL-UNNAMED',
                '--add-exports=java.base/sun.security.util=ALL-UNNAMED']
}
