plugins {
    id 'java'
    alias(libs.plugins.spring.boot)
}

dependencies {
    implementation platform(libs.spring.boot.bom)
    testImplementation platform(libs.spring.boot.bom)
    implementation project(':backend:platform')
    implementation project(':backend:auth')
    implementation project(':backend:audit')
    implementation project(':backend:draft')
    implementation project(':backend:data-integration')
    implementation project(':backend:dw-gateway-client')
    implementation project(':backend:file-core')
    implementation project(':backend:policy')
    implementation libs.spring.boot.starter.web
    implementation libs.spring.boot.starter.data.jpa
    implementation libs.spring.boot.starter.aop
    implementation libs.spring.boot.starter.actuator
    implementation libs.bundles.spring.boot.security
    implementation libs.spring.boot.starter.validation
    implementation libs.springdoc.openapi.starter
    implementation libs.spring.boot.starter.cache
    implementation libs.spring.boot.starter.data.redis
    implementation libs.caffeine
    implementation libs.spring.kafka
    implementation libs.tika.core
    implementation libs.jackson.dataformat.yaml
    implementation libs.micrometer.tracing.bridge.otel
    implementation libs.opentelemetry.exporter.otlp
    runtimeOnly libs.h2.database
    compileOnly libs.lombok
    annotationProcessor libs.lombok
    testCompileOnly libs.lombok
    testAnnotationProcessor libs.lombok

    testImplementation libs.bundles.spring.test
    testImplementation libs.spring.boot.testcontainers
    testImplementation libs.testcontainers.junit
    testImplementation libs.junit.platform.launcher
    testImplementation libs.embedded.redis
}

tasks.withType(Test).configureEach {
    systemProperty 'spring.task.scheduling.enabled', 'false'
    exclude '**/ApplicationTests.class'
}

// server 모듈은 감사/마스킹/정책/익스포트 경로 중심으로 커버리지 측정 범위를 축소해 실질적인 검증에 집중
afterEvaluate {
    def includePackages = ['com/example/server/export/**',
                           'com/example/server/policy/**',
                           'com/example/server/audit/**']

    tasks.named('jacocoTestReport').configure {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, include: includePackages)
        }))
    }

    tasks.named('jacocoTestCoverageVerification').configure {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, include: includePackages)
        }))
    }
}
